plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.26'
    id "io.qameta.allure" version "2.12.0"
}

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.1'
    assertjVersion = '3.26.3'
    guiceVersion = '7.0.0'
    allureVersion = '2.29.0'
    playwrightVersion = '1.50.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

checkstyle {
    toolVersion = '10.18.2'
    configFile = file("src/main/resources/checkstyle.xml")
}

spotbugs {
    toolVersion = '4.8.6'
    excludeFilter = file("${project.rootDir}/src/main/resources/excludeFilter.xml")
}

allure {
    useJUnit5 {
        version = allureVersion
    }
}

dependencies {
    implementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    implementation "org.assertj:assertj-core:${assertjVersion}"
    implementation "com.microsoft.playwright:playwright:${playwrightVersion}"
    implementation "com.google.inject:guice:${guiceVersion}"

}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(21)
}

tasks.named("spotbugsMain").configure {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.named("spotbugsTest").configure {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.withType(Checkstyle) {
    reports {
        html.required.set(true)
        xml.required.set(false)
    }
}

tasks.named("test") {
    dependsOn("checkstyleMain", "checkstyleTest", "spotbugsMain", "spotbugsTest")
}

test {
    useJUnitPlatform()
    systemProperty 'browser', System.getProperty('browser', 'chrome')
    systemProperty 'browserVersion', System.getProperty('browserVersion', '128.0')
    systemProperty 'browserSize', System.getProperty('browserSize', '1920x1080')
    systemProperty 'baseUrl', System.getProperty('baseUrl', 'https://otus.ru')
}